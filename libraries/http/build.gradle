/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 *
 */

description = "Apache Zest™ Http Library provides embedded Jetty for Apache Zest™ application use."

jar { manifest { name = "Apache Zest™ Library - Http" }}

dependencies {

  compile( project( ":org.apache.zest.core:org.apache.zest.core.api" ) )
  compile( project( ":org.apache.zest.libraries:org.apache.zest.library.constraints" ) )
  compile( project( ":org.apache.zest.libraries:org.apache.zest.library.jmx" ) )
  compile( libraries.bouncy_castle )
  compile( libraries.jetty_webapp )
  compile( libraries.jetty_jmx )

  testCompile( project( ":org.apache.zest.core:org.apache.zest.core.testsupport" ) )
  testCompile( libraries.http_client )

  testRuntime( project( ":org.apache.zest.core:org.apache.zest.core.runtime" ) )
  testRuntime( libraries.logback )

}

// Do not run vhost tests on Java >= 9 because they use internal JDK apis
if(JavaVersion.current() < JavaVersion.VERSION_1_9) {
    sourceSets {
        vhostTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/vhost-test/java')
            }
            resources {
                srcDir file('src/vhost-test/resources')
            }
        }
    }
    configurations {
        vhostTestCompile.extendsFrom testCompile
        vhostTestRuntime.extendsFrom testRuntime
    }
    dependencies {
        vhostTestCompile( libraries.dnsjava )
    }
    task vhostTest(type: Test) {
        testClassesDir = sourceSets.vhostTest.output.classesDir
        classpath = sourceSets.vhostTest.runtimeClasspath
        systemProperties 'networkaddress.cache.ttl': '0'
        systemProperties 'sun.net.spi.nameservice.provider.1': 'dns,LocalManagedDns'
    }
    check.dependsOn vhostTest
    vhostTest.mustRunAfter test
}
